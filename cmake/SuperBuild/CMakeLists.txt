cmake_minimum_required(VERSION 3.22)

project(
    toucan-SuperBuild
    VERSION 0.1.0
    DESCRIPTION "toucan superbuild"
    HOMEPAGE_URL "https://github.com/darbyjohnston/toucan"
    LANGUAGES CXX C)

set(toucan_ZLIB ON CACHE BOOL "Build ZLIB")
set(toucan_Freetype ON CACHE BOOL "Build Freetype")
set(toucan_PNG ON CACHE BOOL "Build PNG")
set(toucan_NASM ON CACHE BOOL "Build NASM")
set(toucan_JPEG ON CACHE BOOL "Build JPEG")
set(toucan_TIFF ON CACHE BOOL "Build TIFF")
set(toucan_Imath ON CACHE BOOL "Build Imath")
set(toucan_OpenEXR ON CACHE BOOL "Build OpenEXR")
set(toucan_FFmpeg ON CACHE BOOL "Build FFmpeg")
set(toucan_FFmpeg_MINIMAL ON CACHE BOOL "Build a minimal set of FFmpeg codecs")
set(toucan_OpenColorIO ON CACHE BOOL "Build OpenColorIO")
set(toucan_OpenImageIO ON CACHE BOOL "Build OpenImageIO")
set(toucan_OpenTimelineIO ON CACHE BOOL "Build OpenTimelineIO")
set(toucan_OpenFX ON CACHE BOOL "Build OpenFX")
set(toucan_dtk ON CACHE BOOL "Build dtk")

list(PREPEND CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR})

if(NOT CMAKE_CXX_STANDARD)
    set(CMAKE_CXX_STANDARD 17)
endif()
if(NOT BUILD_SHARED_LIBS)
    set(BUILD_SHARED_LIBS OFF)
endif()
if(NOT CMAKE_POSITION_INDEPENDENT_CODE)
    set(CMAKE_POSITION_INDEPENDENT_CODE ON)
endif()
if(MSVC)
    set(CMAKE_MSVC_RUNTIME_LIBRARY MultiThreaded$<$<CONFIG:Debug>:Debug>DLL)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /MP")
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} /MP")
endif()
if(APPLE)
    set(CMAKE_FIND_FRAMEWORK LAST)
    set(CMAKE_IGNORE_PREFIX_PATH /opt/homebrew)
endif()

set(toucan_EXTERNAL_PROJECT_ARGS
    -DCMAKE_MODULE_PATH=${CMAKE_MODULE_PATH}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
    -DCMAKE_PREFIX_PATH=${CMAKE_PREFIX_PATH}
    -DCMAKE_IGNORE_PREFIX_PATH=${CMAKE_IGNORE_PREFIX_PATH}
    -DCMAKE_FIND_FRAMEWORK=${CMAKE_FIND_FRAMEWORK}
    -DCMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD}
    -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
    -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
    -DCMAKE_MSVC_RUNTIME_LIBRARY=${CMAKE_MSVC_RUNTIME_LIBRARY}
    -DCMAKE_VERBOSE_MAKEFILE=${CMAKE_VERBOSE_MAKEFILE}
    -DCMAKE_OSX_DEPLOYMENT_TARGET=${CMAKE_OSX_DEPLOYMENT_TARGET}
    -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES}
    -DBUILD_SHARED_LIBS=${BUILD_SHARED_LIBS}
    -DCMAKE_POSITION_INDEPENDENT_CODE=${CMAKE_POSITION_INDEPENDENT_CODE})

if(toucan_ZLIB)
    include(BuildZLIB)
endif()
if(toucan_Freetype)
    include(BuildFreetype)
endif()
if(toucan_PNG)
    include(BuildPNG)
endif()
if(toucan_NASM AND NOT WIN32)
    include(BuildNASM)
endif()
if(toucan_JPEG)
    include(BuildJPEG)
endif()
if(toucan_TIFF)
    include(BuildTIFF)
endif()
if(toucan_Imath)
    include(BuildImath)
endif()
if(toucan_OpenEXR)
include(BuildOpenEXR)
endif()
if(toucan_FFmpeg)
    include(BuildFFmpeg)
endif()
if(toucan_OpenColorIO)
    include(Buildexpat)
    include(Buildminizip-ng)
    include(Buildpystring)
    include(Buildyaml-cpp)
    include(BuildOpenColorIO)
endif()
if(toucan_OpenImageIO)
    include(BuildOpenImageIO)
endif()
if(toucan_OpenTimelineIO)
    include(BuildOpenTimelineIO)
endif()
if(toucan_OpenFX)
    include(BuildOpenFX)
endif()
if(toucan_dtk)
    include(Builddtk-deps)
    include(Builddtk)
endif()
